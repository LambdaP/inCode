Writer Tutorial
---------------

This used to be a part of my [streaming huffman compression part 2][original]
post.  I realized that Writer was actaully a bad idea and an anti-pattern for
this part, so I've removed it and written a much cleaner Monoid interface.
However, I offer this up saved here for anyone who is interested in learning
how (not) to use Writer :)

[original]: http://blog.jle.im/entry/streaming-huffman-compression-in-haskell-part-2-binary


### Pre-searching

We can sort of "solve" both of these problems this by traversing through our
`PreTree` and adding an entry to a `Map` at every leaf.  This fixes our
repetition problem by memozing all of our results into a map...and it fixes
our search problem because `Map`s are an ordered binary search tree with
efficient O(log n) lookups.

We can do this using the `State` monad as we learned about in [the previous
post][state], by using a `Map a Encoding` as an underlying state and adding to
it using `modify` at every step.

[state]: http://blog.jle.im/entry/streaming-huffman-compression-in-haskell-part-1-trees#the-state-monad

However, let's note something interesting --- all we ever "do" to the state is
"add" to it.  We never `get` it, and we *never* need to "branch" on the
results of our stateful actions.  Our stateful actions don't even return
anything ever!

We said earlier that, for things that don't require "branching", one can use a
fold instead of State.  But a fold in this case might be a little unweildy,
because each "write" depends on how deep in the tree you are.

Let's look at our next big general useful monad: Writer.

#### The Writer Monad

Here are some key flags that you might want a Writer:

1.  All you ever do is some sort of "append" or "adding" to your underlying
    state.
3.  You never need to decide anything based on the current state.
4.  Your entire computation revolves around building up a giant thing by
    continuously adding little things to it.
5.  Your "final computation" returns `()` in the end.

(Note that these things are signs you might want to use a Writer monad, but
actually, the Writer monad has more uses than just the ones described above.)

The Writer monad is actually a lot simpler than the State monad, so don't
worry!

Like `State`, `Writer` is also a newtype wrapper; let's compare them!

~~~haskell
newtype State  s a = State (s -> (a, s))
newtype Writer w a = Writer      (a, w)
~~~

And so it looks like `Writer` is just a "dumb `State`".  It's a `State` that
*doesn't have direct access to its state*.

Semantically, we say that `(a, w)` (or `Writer w a`) is a Writer with a
*result* `a` and an *accumulator* `w`.  `a` is the type of the result of the
Writer, and `w` is the type of our accumulator.  We can call them "data with
attached accumulators".

So what would `andThen` look like, for `(a, w)`?  How can we "sequence" two
"data with attached accumulators"?  Well...we can simply combine their
accumulators!

~~~haskell
andThen :: Monoid w => (a, w) -> (b, w) -> (a, w)
(_, acc) `andThen` (x, acc') = (x, acc <> acc')
~~~

I'm assuming you already know about the Monoid typeclass and `<>`.  If you
don't, then to put it briefly, a `Monoid` is something that implements `<>`
("`mappend`"):

~~~haskell
(<>) :: Monoid a => a -> a -> a
~~~

which is an associative "combining function".  "Give me two `a`'s and I'll
give you a combined `a`."  It is important that this combining operator is
associative, meaning that `x <> (y <> z)` is the same as `(x <> y) <> z`.

Monoids also implement `mempty :: Monoid a => a`, which is the element that,
when `<>`'d with something else, does not change that something else.

The canonical example of a Monoid is the list `[a]`, also known as the "free
monoid".  For lists, `<>` is `++` and `mempty` is `[]`:

~~~haskell
x ++ (y ++ z) == (x ++ y) ++ z
      x ++ [] == x
      [] ++ x == x
~~~

Basically, monoids represent "combinable things".

So, when we `andThen` two writer tuples, we combine the `w`.

I'll leave `andThenWith :: (a, w) -> (a -> (b, w)) -> (b, w)`  up to you to
try to implement yourself.

There's also `return`:

~~~haskell
return :: Monoid w => a -> (a, w)
return x = (x, mempty)
~~~

Where `return` just has the item with an empty/"fresh" accumulator.

Again, in real life, we can't define a typeclass instance on a type synonym,
and we actually can't even define an instance directly on `(a, w)` (can you
see why?).  So again, the canonical implementation comes in the
[transformers][] library, and wraps the `(a, w)` in a newtype as `Writer w a`,
just like for State.  It also offers a few nice primitives, but we will only
be using one:

[transformers]: http://hackage.haskell.org/package/transformers

~~~haskell
tell :: Monoid w => w -> Writer w ()
tell y = Writer ((), y)
~~~

Basically, `tell` is just a simple `Writer` that returns no result, but adds
something that will be `<>`'d to the accumulator.

##### Sample Writer

Let's take a whack at an example computation using the Writer monad.

Let's re-write a simple fold that goes down a list and adds up every even
number in that list.

~~~haskell
addEven :: Integral a => a -> Writer (Sum a) ()
addEven x | even x    = tell (Sum x)
          | otherwise = return ()

addAllEvens :: Integral a => [a] -> Writer (Sum a) ()
addAllEvens []     = return ()
addAllEvens (x:xs) = do
    addEven x
    addAllEvens xs

-- or, using higher order functions
addAllEvens' :: Integral a => [a] -> Writer (Sum a) ()
addAllEvens' = mapM_ addEven

runAddAllEvens :: Integral a => [a] -> Sum a
runAddAllEvens = execWriter . addAllEvens
~~~

~~~haskell
Î»: runAddAllEvens [2,7,3,5,4,7,8]
Sum 14
~~~

`Sum a` (where `a` is a `Num`) is a Monoid, whose "combining"/"merging"
function is simply to add the values inside.  `Sum 1 <> Sum 2 == Sum 3`.
`mempty` is, of course, `Sum 0`.

`execWriter` unwraps the `Writer` newtype, and returns only the `w`.

#### Pre-searching, with Writer

With this in mind, let's build up our memoized lookup tree.

~~~haskell
ptTable :: Ord a => PreTree a -> Map a Encoding
ptTable pt = execWriter (go pt [])
  where
    go (PTLeaf x) enc       = tell (x `M.singleton` reverse enc)
    go (PTNode pt1 pt2) enc = go pt1 (DLeft  : enc) >>
                              go pt2 (DRight : enc)
~~~

We take advantage of the fact that `Map k v` is in fact a monoid, and that
`map1 <> map2` means "adding" the two maps together; it *merges* the maps.

``k `singleton` v`` creates a new map with only one entry (`k` and `v`), so
when we "append" ``k `singleton` v`` to a map, it's like simply adding a
key/value pair to the map.

Notice that this has pretty much the exact same structure as our previous
depth-first search:

~~~haskell
findPT :: Eq a => PreTree a -> a -> Maybe Encoding
findPT pt0 x = go pt0 []
  where
    go (PTLeaf y      ) enc | x == y    = Just (reverse enc)
                            | otherwise = Nothing
    go (PTNode pt1 pt2) enc = go pt1 (DLeft  : enc) <|>
                              go pt2 (DRight : enc)
~~~

Except instead of returning a value based on equality at the leaves, we
"write" it.  And instead of "choosing between" the two branches of a node with
`(<|>)`, we "sequence"/do them both with `(>>)`.


