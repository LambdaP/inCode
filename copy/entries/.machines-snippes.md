#### on State

Before we move on, let's take a slight diversion --- you can skip this whole
section if you wish, to get onto the main point.

Notice that in our last example, our "state" `n` was the same type as our
"output" `n+1`.  Is this in general the case?

The type of our stream is `Stream Int`...does `Int` refer to the state or the
output?

As it turns out, this `Int` refers to the output --- it is the type of the
"head" of the stream, and all values we will eventually grab from it.

Note however that the type of the state is not actually included in the type
signature, so it can be whatever you want!  As a trivial example, let's say
that `streamFrom` actually takes a `x :: Double` instead of an `n :: Int`, and
rounds it before it pops it out as the "head":

~~~haskell
myStream' :: Stream Int
myStream' = streamFrom' 1.0
  where
    streamFrom' :: Double -> Stream Int
    streamFrom' x = SCons ( round x, streamFrom' (x+1) )
~~~

This function now sorta behaves similarly to our original `streamFrom`...
`myStream'` would still be `Stream Int` and output ints, and we might not ever
be able to tell that the internal state of `myStream'` was "actually" a
double!

Now also observe that the internal state is actually in general *inaccessible*
and *opaque* to the outside world.  What if we had `streamFrom` simply return
whether or not `n` was even?

~~~haskell
myBoolStream :: Stream Bool
myBoolStream = boolStreamFrom 1
  where
    boolStreamFrom :: Int -> Stream Bool
    boolStreamFrom n = SCons ( even n, boolStreamFrom (n+1) )
~~~

~~~haskell
λ: take 5 $ streamToList myBoolStream
[False,True,False,True,False]
~~~

`myBoolStream` simply cycles between `False` and `True`.  Yet, it has an
internal state that is completely closed off to us that is an `Int` counting
from `1` to infinity.  We might not have ever even known.

This property --- that the states of these types of machines are hidden from
the world --- is actually going to be very useful.  Like I said before, every
machine can really be considered self-contained.  This is unlike using a State
monad-based loop, where all internal state is more or less freely viewable
manipulatable by anyone.  Here, every machine is truly its own little world.

In fact, because the type of the state is unknown and unpredictable...even if
we could "force" the state out of a stream somehow, we wouldn't even be able
to work with it in a type safe way.  The type is truly dynamic and the type of
the nth state of a stream is unknowable at compile time.

Here is a stream whose state switches from an `Int` to a `Bool` dynamically.

~~~
wackyStateStream :: Stream (Maybe Int)
wackyStateStream = wackyStateBool True
  where
    wackyStateBool :: Bool -> Stream (Maybe Int)
    wakcyStateBool False  = SCons (Nothing , wackyStateBool True)
    wackyStateBool True   = SCons (Just 100, wackyStateInt 8)

    wackyStateInt :: Int -> Stream (Maybe Int)
    wackyStateInt n
        | n `mod` 7 == 0  = SCons (Just n, wackyStateBool True)
        | otherwise       = SCons (Just (n+2), wackyStateInt (n+3))
~~~

~~~
λ: take 7 $ streamToList wackyStateStream
[Nothing, Just 100, Just 8, Just 11, Just 16, Nothing, Just 100]
~~~


------------------------

Here is an Auto that outputs a rolling average of the values it accumulates,
with a given window size.

~~~haskell
rollingAverage :: forall a. Fractional a
    => Int          -- length of the window
    -> Auto a a     -- an Auto taking an `a` and returning an average `a`
rollingAverage window = roll []
  where
    roll :: [a] -> Auto a a
    roll xs = ACons $ \val ->
      let xs' = take window $ val:xs  -- pop on the new value, drop all
                                      --   values past the window
          ave = sum xs' / genericLength xs'  -- the average
      in  ( ave, roll xs' )
~~~

~~~haskell
λ: testAuto_ (rollingAverage 4) [2,8,4,5,1,8,3,5,1,1,8,3,5,9,2]
[2.0 ,5.0 ,4.67,4.75,4.5
,4.5 ,4.25,4.25,4.25,2.5
,3.75,3.25,4.25,6.25,4.75]
~~~

Here is an Auto that is always `False`...except whenever it receives a signal
matching a given predicate (if it is "triggered"), it remains `True` for a
specified amount of time.

~~~haskell
onFor :: forall a.
     (a -> Bool)  -- test to see if an input 'triggers'
  -> Int          -- amount of time to stay True for
  -> Auto a Bool  -- An Auto that takes an `a` and returns a `Bool`
onFor p hold = wait
  where
    wait :: Auto a Bool                 -- the "waiting" state
    wait = ACons $ \input ->
      if p input                        -- if triggered,
        then (True, countdown (hold-1)) -- jump to "countdown" state
        else (False, wait)              -- otherwise, stay waiting

    countdown :: Int -> Auto a Bool     -- the "countdown" state
    countdown n = ACons $ \input ->
      if p input                        -- if re-triggered
        then (True, countdown (hold-1)) -- countdown all over again
        else
          if n == 1
            then (False, wait)          -- If counted down, go wait again
            else (True, countdown (n-1))  -- otherwise, count down.
~~~

~~~haskell
λ: :t onFor even 3
onFor even 3 :: Auto Int Bool
λ: testAuto_ (onFor even 3) [1,1,2,1,1,1,1,4,1,6,1,1,1,1]
[ False, False, True , True,True
, False, True , True , True,True
, True , False, False ]
~~~

Here is an auto that keeps a Map as its state, and takes as input a data type
`data Command k v = Insert k v | Lookup k | Delete k` allows you to either
insert, lookup, or delete an item.  However, the map has a "maximum size".  If
you insert past the maximum size, the insert will fail and you'll receive a
`Nothing`.  Otherwise, you'll receive a `Just val` with the value you just
inserted.  Successful lookups will return `Just val`, or `Nothing` on a
failure.  Same for deletions.

~~~haskell
import qualified Data.Map.Strict as Map

data Command k v = Insert k v | Lookup k | Delete k

autoMap :: forall k v. Ord k
    => Int              -- the maximum capacity of the map
    -> Auto (Command k v) (Maybe v)
autoMap cap = go Map.empty
  where
    go :: Map.Map k v -> Auto (Command k v) (Maybe v)
    go m = ACons $ \command ->
      case command of
        Insert key val ->
          if Map.size m >= cap && key `Map.notMember` m
            then
              ( Nothing, go m )                 -- Map is full, no go!
            else
              let m' = Map.insert key val m     -- go for it!
              in  ( Just val, go m' )
        Lookup key ->
          ( key `Map.lookup` m, go m )
        Delete key ->
          let result  = key `Map.lookup` m
              m'      = key `Map.delete` m
          in  ( result, go m' )
~~~

~~~haskell
λ: testAuto_ (autoMap 3)
  |  [ Insert "hello" 7
  |  , Insert "world" 10
  |  , Insert "foo" 12
  |  , Insert "bar" 15
  |  , Delete "baz"
  |  , Delete "world"
  |  , Insert "haskell" 19
  |  , Lookup "world"
  |  , Lookup "hello"
  |  ]
[ Just 7 , Just 10, Just 12
, Nothing, Nothing, Just 10
, Just 19, Nothing, Just 7  ]
~~~



--------------------------------------


<aside>
    ###### Aside

And now, another diversion.  This is actually a pretty big one, so if you are
still digesting the rest of the post, there is no problem with completely
skipping this aside :)

Recall the function `scanl :: (b -> a -> b) -> b -> [a] -> [b]`.  `scanl` is
just like `foldl`, except that it "keeps track" of the history of the
accumulator.

For example:

~~~haskell
λ: foldl (+) 0 [1..10]
55
λ: scanl (+) 0 [1..10]
[1,3,6,10,15,21,28,36,45,55]
~~~

(in real life, `scanl` also adds the initial value of the accumulator to the
head of the list, but we'll pretend here that it doesn't)

One way to think about scan is that scan is "map with memory".  With `map`,
you apply a memoryless function to every element in a list.  With `scanl`,
you apply a function to every element in a list...but this function
"remembers" the values of the elements it has already "mapped".

This is very apparent when we examine the two type signatures:

~~~haskell
map   f       :: [a] -> [b]
scanl op init :: [a] -> [b]
~~~

While `map` takes a function and returns a "mapper" (`[a] -> [b]`), `scanl`
takes an *operator and an initial value* and returns a "mapper".

They both take *something* and return a "mapper".

But wait!  We have seen something before with this *exact* same type
signature: `testAuto_ auto`!

~~~haskell
map       f       :: [a] -> [b]
scanl     op init :: [a] -> [b]
testAuto_ auto    :: [a] -> [b]
~~~

Huh.  Interesting!

Actually, if we think about it...any `scanl op init` behaves *exactly the
same* as a `testAuto_ auto`, for some value of `auto` (Actually, that `auto`
is exactly `foldAuto op init`).  `testAuto_ auto` is some sort of
"mapper"...but *with memory* --- just like `scanl op init`!

Isn't this what we said that Auto was?  A function with memory?  Is an `Auto a
b` equivalent to a `(b -> a -> b)` + `b` combination?  Are all Autos
equivalent to a scan of some sort?  We see that every `scanl op init` be
recreated with a corresponding `auto` in `testAuto_ auto`, `auto = foldAuto op
init`.  But can every `testAuto_ auto` be recreated with a proper choice of
`op` and `init` in `scanl op init`?

If not, what can you "add" to `scanl op init` to give it the same power as
`testAuto_ auto`?

Consider the curious fact we mentioned before.  In an `Auto a b`, the type of
the state is not mentioned and is possibly dynamic.  A `scanl op init` also
involves only two types, `a` and `b`.  Where is the type of the state in
`scanl op init`? Is it fixed, or is it free like for Autos?

I'll leave these questions to you, the reader.  Leave an answer in the
comments if you want!
</aside>




<!-- But I'll leave this for the next part. -->

<!-- For now, think of what it means to be "function like".  Not only do you have -->
<!-- inputs and outputs...but there are also certain things about being a function -->
<!-- that are key to its nature.  Things like...function composition, maybe?  What -->
<!-- would that even look like with our autos? -->

<!-- Okay, I think I've belabored the point enough.  It should come as no surprise -->
<!-- to you that this "function like" concept has a name in Haskell. -->

<!-- These "function things" are all members of the `Category` typeclass. -->
<!-- Technically, they represent morphisms in the mathematical concept of a -->
<!-- category.  To prevent confusion, I will usually refer to them as morphisms or -->
<!-- as "arrows". -->

<!-- <aside> -->
<!--     ###### Aside -->

<!-- Technically, there is actually a separate `Arrow` typeclass.  This typeclass -->
<!-- more or less provides convenient functions and combinators for chaining and -->
<!-- composing categories, so for now and forever, I will mostly use the words -->
<!-- "arrow" and "morphism" interchangeably.  And sometimes, if I slip, "category". -->
<!-- However, this distinction should be made very clear. -->
<!-- </aside> -->

<!-- #### Semantics of categories -->

<!-- So what does it mean to be a category morphism? -->

<!-- First of all, and most importantly of all, morphisms can be **composed**. -->

<!-- For example, if you have two functions `f :: (->) b c` and `g :: (->) a b`, -->
<!-- you should be able to have some way of "chaining" them --- first apply `g`, -->
<!-- then apply `f`.  Baiscally, `f . g` produces a *new* function from `a` to `c`. -->
<!-- It takes an `a` to `b` functiona and a `b` to `c` function and composes them -->
<!-- into a brand new `a` to `c` function. -->

<!-- Apart from composition, categories must also provide *identity morphisms*. -->
<!-- That is, given any morphism `f :: Category r => r a b`, your category has to -->
<!-- provide for you a left-hand "post-apply" identity `id :: r b b` such that `id -->
<!-- . f` is the same as just `f` alone, as well as a right-hand "pre-apply" -->
<!-- identity `id :: r a a` such that `f . id` is the same as just `f` alone. -->

<!-- The Category typeclass in Haskell exactly provides just the composition `(.)` -->
<!-- and the "identity generator" `id`. -->


