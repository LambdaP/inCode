#### on State

Before we move on, let's take a slight diversion --- you can skip this whole
section if you wish, to get onto the main point.

Notice that in our last example, our "state" `n` was the same type as our
"output" `n+1`.  Is this in general the case?

The type of our stream is `Stream Int`...does `Int` refer to the state or the
output?

As it turns out, this `Int` refers to the output --- it is the type of the
"head" of the stream, and all values we will eventually grab from it.

Note however that the type of the state is not actually included in the type
signature, so it can be whatever you want!  As a trivial example, let's say
that `streamFrom` actually takes a `x :: Double` instead of an `n :: Int`, and
rounds it before it pops it out as the "head":

~~~haskell
myStream' :: Stream Int
myStream' = streamFrom' 1.0
  where
    streamFrom' :: Double -> Stream Int
    streamFrom' x = SCons ( round x, streamFrom' (x+1) )
~~~

This function now sorta behaves similarly to our original `streamFrom`...
`myStream'` would still be `Stream Int` and output ints, and we might not ever
be able to tell that the internal state of `myStream'` was "actually" a
double!

Now also observe that the internal state is actually in general *inaccessible*
and *opaque* to the outside world.  What if we had `streamFrom` simply return
whether or not `n` was even?

~~~haskell
myBoolStream :: Stream Bool
myBoolStream = boolStreamFrom 1
  where
    boolStreamFrom :: Int -> Stream Bool
    boolStreamFrom n = SCons ( even n, boolStreamFrom (n+1) )
~~~

~~~haskell
λ: take 5 $ streamToList myBoolStream
[False,True,False,True,False]
~~~

`myBoolStream` simply cycles between `False` and `True`.  Yet, it has an
internal state that is completely closed off to us that is an `Int` counting
from `1` to infinity.  We might not have ever even known.

This property --- that the states of these types of machines are hidden from
the world --- is actually going to be very useful.  Like I said before, every
machine can really be considered self-contained.  This is unlike using a State
monad-based loop, where all internal state is more or less freely viewable
manipulatable by anyone.  Here, every machine is truly its own little world.

In fact, because the type of the state is unknown and unpredictable...even if
we could "force" the state out of a stream somehow, we wouldn't even be able
to work with it in a type safe way.  The type is truly dynamic and the type of
the nth state of a stream is unknowable at compile time.

Here is a stream whose state switches from an `Int` to a `Bool` dynamically.

~~~
wackyStateStream :: Stream (Maybe Int)
wackyStateStream = wackyStateBool True
  where
    wackyStateBool :: Bool -> Stream (Maybe Int)
    wakcyStateBool False  = SCons (Nothing , wackyStateBool True)
    wackyStateBool True   = SCons (Just 100, wackyStateInt 8)

    wackyStateInt :: Int -> Stream (Maybe Int)
    wackyStateInt n
        | n `mod` 7 == 0  = SCons (Just n, wackyStateBool True)
        | otherwise       = SCons (Just (n+2), wackyStateInt (n+3))
~~~

~~~
λ: take 7 $ streamToList wackyStateStream
[Nothing, Just 100, Just 8, Just 11, Just 16, Nothing, Just 100]
~~~


------------------------

Here is an Auto that outputs a rolling average of the values it accumulates,
with a given window size.

~~~haskell
rollingAverage :: forall a. Fractional a
    => Int          -- length of the window
    -> Auto a a     -- an Auto taking an `a` and returning an average `a`
rollingAverage window = roll []
  where
    roll :: [a] -> Auto a a
    roll xs = ACons $ \val ->
      let xs' = take window $ val:xs  -- pop on the new value, drop all
                                      --   values past the window
          ave = sum xs' / genericLength xs'  -- the average
      in  ( ave, roll xs' )
~~~

~~~haskell
λ: testAuto_ (rollingAverage 4) [2,8,4,5,1,8,3,5,1,1,8,3,5,9,2]
[2.0 ,5.0 ,4.67,4.75,4.5
,4.5 ,4.25,4.25,4.25,2.5
,3.75,3.25,4.25,6.25,4.75]
~~~

Here is an Auto that is always `False`...except whenever it receives a signal
matching a given predicate (if it is "triggered"), it remains `True` for a
specified amount of time.

~~~haskell
onFor :: forall a.
     (a -> Bool)  -- test to see if an input 'triggers'
  -> Int          -- amount of time to stay True for
  -> Auto a Bool  -- An Auto that takes an `a` and returns a `Bool`
onFor p hold = wait
  where
    wait :: Auto a Bool                 -- the "waiting" state
    wait = ACons $ \input ->
      if p input                        -- if triggered,
        then (True, countdown (hold-1)) -- jump to "countdown" state
        else (False, wait)              -- otherwise, stay waiting

    countdown :: Int -> Auto a Bool     -- the "countdown" state
    countdown n = ACons $ \input ->
      if p input                        -- if re-triggered
        then (True, countdown (hold-1)) -- countdown all over again
        else
          if n == 1
            then (False, wait)          -- If counted down, go wait again
            else (True, countdown (n-1))  -- otherwise, count down.
~~~

~~~haskell
λ: :t onFor even 3
onFor even 3 :: Auto Int Bool
λ: testAuto_ (onFor even 3) [1,1,2,1,1,1,1,4,1,6,1,1,1,1]
[ False, False, True , True,True
, False, True , True , True,True
, True , False, False ]
~~~

Here is an auto that keeps a Map as its state, and takes as input a data type
`data Command k v = Insert k v | Lookup k | Delete k` allows you to either
insert, lookup, or delete an item.  However, the map has a "maximum size".  If
you insert past the maximum size, the insert will fail and you'll receive a
`Nothing`.  Otherwise, you'll receive a `Just val` with the value you just
inserted.  Successful lookups will return `Just val`, or `Nothing` on a
failure.  Same for deletions.

~~~haskell
import qualified Data.Map.Strict as Map

data Command k v = Insert k v | Lookup k | Delete k

autoMap :: forall k v. Ord k
    => Int              -- the maximum capacity of the map
    -> Auto (Command k v) (Maybe v)
autoMap cap = go Map.empty
  where
    go :: Map.Map k v -> Auto (Command k v) (Maybe v)
    go m = ACons $ \command ->
      case command of
        Insert key val ->
          if Map.size m >= cap && key `Map.notMember` m
            then
              ( Nothing, go m )                 -- Map is full, no go!
            else
              let m' = Map.insert key val m     -- go for it!
              in  ( Just val, go m' )
        Lookup key ->
          ( key `Map.lookup` m, go m )
        Delete key ->
          let result  = key `Map.lookup` m
              m'      = key `Map.delete` m
          in  ( result, go m' )
~~~

~~~haskell
λ: testAuto_ (autoMap 3)
  |  [ Insert "hello" 7
  |  , Insert "world" 10
  |  , Insert "foo" 12
  |  , Insert "bar" 15
  |  , Delete "baz"
  |  , Delete "world"
  |  , Insert "haskell" 19
  |  , Lookup "world"
  |  , Lookup "hello"
  |  ]
[ Just 7 , Just 10, Just 12
, Nothing, Nothing, Just 10
, Just 19, Nothing, Just 7  ]
~~~



--------------------------------------


<aside>
    ###### Aside

And now, another diversion.  This is actually a pretty big one, so if you are
still digesting the rest of the post, there is no problem with completely
skipping this aside :)

Recall the function `scanl :: (b -> a -> b) -> b -> [a] -> [b]`.  `scanl` is
just like `foldl`, except that it "keeps track" of the history of the
accumulator.

For example:

~~~haskell
λ: foldl (+) 0 [1..10]
55
λ: scanl (+) 0 [1..10]
[1,3,6,10,15,21,28,36,45,55]
~~~

(in real life, `scanl` also adds the initial value of the accumulator to the
head of the list, but we'll pretend here that it doesn't)

One way to think about scan is that scan is "map with memory".  With `map`,
you apply a memoryless function to every element in a list.  With `scanl`,
you apply a function to every element in a list...but this function
"remembers" the values of the elements it has already "mapped".

This is very apparent when we examine the two type signatures:

~~~haskell
map   f       :: [a] -> [b]
scanl op init :: [a] -> [b]
~~~

While `map` takes a function and returns a "mapper" (`[a] -> [b]`), `scanl`
takes an *operator and an initial value* and returns a "mapper".

They both take *something* and return a "mapper".

But wait!  We have seen something before with this *exact* same type
signature: `testAuto_ auto`!

~~~haskell
map       f       :: [a] -> [b]
scanl     op init :: [a] -> [b]
testAuto_ auto    :: [a] -> [b]
~~~

Huh.  Interesting!

Actually, if we think about it...any `scanl op init` behaves *exactly the
same* as a `testAuto_ auto`, for some value of `auto` (Actually, that `auto`
is exactly `foldAuto op init`).  `testAuto_ auto` is some sort of
"mapper"...but *with memory* --- just like `scanl op init`!

Isn't this what we said that Auto was?  A function with memory?  Is an `Auto a
b` equivalent to a `(b -> a -> b)` + `b` combination?  Are all Autos
equivalent to a scan of some sort?  We see that every `scanl op init` be
recreated with a corresponding `auto` in `testAuto_ auto`, `auto = foldAuto op
init`.  But can every `testAuto_ auto` be recreated with a proper choice of
`op` and `init` in `scanl op init`?

If not, what can you "add" to `scanl op init` to give it the same power as
`testAuto_ auto`?

Consider the curious fact we mentioned before.  In an `Auto a b`, the type of
the state is not mentioned and is possibly dynamic.  A `scanl op init` also
involves only two types, `a` and `b`.  Where is the type of the state in
`scanl op init`? Is it fixed, or is it free like for Autos?

I'll leave these questions to you, the reader.  Leave an answer in the
comments if you want!
</aside>




But I'll leave this for the next part.

For now, think of what it means to be "function like".  Not only do you have
inputs and outputs...but there are also certain things about being a function
that are key to its nature.  Things like...function composition, maybe?  What
would that even look like with our autos?

Okay, I think I've belabored the point enough.  It should come as no surprise
to you that this "function like" concept has a name in Haskell.

These "function things" are all members of the `Category` typeclass.
Technically, they represent morphisms in the mathematical concept of a
category.  To prevent confusion, I will usually refer to them as morphisms or
as "arrows".

<aside>
    ###### Aside

Technically, there is actually a separate `Arrow` typeclass.  This typeclass
more or less provides convenient functions and combinators for chaining and
composing categories, so for now and forever, I will mostly use the words
"arrow" and "morphism" interchangeably.  And sometimes, if I slip, "category".
However, this distinction should be made very clear.
</aside>

#### Semantics of categories

So what does it mean to be a category morphism?

First of all, and most importantly of all, morphisms can be **composed**.

For example, if you have two functions `f :: (->) b c` and `g :: (->) a b`,
you should be able to have some way of "chaining" them --- first apply `g`,
then apply `f`.  Baiscally, `f . g` produces a *new* function from `a` to `c`.
It takes an `a` to `b` functiona and a `b` to `c` function and composes them
into a brand new `a` to `c` function.

Apart from composition, categories must also provide *identity morphisms*.
That is, given any morphism `f :: Category r => r a b`, your category has to
provide for you a left-hand "post-apply" identity `id :: r b b` such that `id
. f` is the same as just `f` alone, as well as a right-hand "pre-apply"
identity `id :: r a a` such that `f . id` is the same as just `f` alone.

The Category typeclass in Haskell exactly provides just the composition `(.)`
and the "identity generator" `id`.





--------------------------------------------------

PART 2
======



As it turns out, this
design pattern has a name.  They're instances of the type class "Category".

Categories
----------

You might have heard of the word "category" applied to Haskell in a few
different contexts.  Don't worry, the Category typeclass isn't scary!  It's
rather simple, actually!  Much simpler than a certain other infamous typeclass
(that rhymes with Shmonad).

The Functor typeclass represents the idea of mapability.  `Maybe`, for
example, is a functor.  `Maybe a`, a concrete type for all `a`, is a concrete
object that "contains" an `a`.

As we will see, `Auto` is a Category.  `Auto a b` is called, for all `a` and
`b`, a *morphism* (or an "arrow", but that is a loaded term in Haskell) in
that category from things of type `a` to things of type `b`.

The Category typeclass represents the very essense of "function-like"-ness.
As it turns out, it includes one special member and one function over
members.

~~~haskell
class Category cat where
    id  :: cat a a
    (.) :: cat b c -> cat a b -> cat a c
~~~

Basically, something is a Category if it has:

1.  A special member (remember, members of Category are called "morphisms" or
    "arrows") that is an "identity function-like"
2.  An operator that takes two arrows and "composes"/sequences them into one
    big one.

These come with three laws:

1.  "Left identity": `id . f == f`
2.  "Right identity": `f . id == f`
3.  "Associativity": `f . (g . h) == (f . g) . h`

<aside>
    ###### Aside

Remember that `id` represents a *concrete member* of the Category, and that
`(.)` represents a *function between members*.

This is similar in pattern to the classic Monoid typeclass:

~~~haskell
class Monoid a where
    mempty :: a
    (<>) :: a -> a -> a
~~~

Where `mempty` describes one specific member, and `(<>)` represents a
binary function between two members.

It's just that for Monoid, our members are concrete "things", but for
Category, our members are morphisms/functions!

Also, remember the Monoid laws?

1. "Left identity": `mempty <> x == x`
2. "Right identity": `x <> mempty == x`
3. "Associativity": `x <> (y <> z) == (x <> y) <> z`

Looks awfully similar!  But can you see why a given Category `cat a b` and
Monoid `a` are not technically the same?
</aside>

But enough with silly maths, let's look at a concrete example.

Basically, this typeclass says that the essense of function-like-ness is the
existence of an identity function-like, and the ability to compose and
sequence functions.

### The `(->)` Category

If categories embody function-like things, then obviously we would expect a
normal function `(->)` to be a Category.

The first item in the Category typeclass is `id`, a member that's an "arrow"
represents an identity function: a function that returns, unchanged, its
input.

~~~haskell
funcId :: (->) a a
funcId = \x -> x
~~~

~~~haskell
λ: funcId 1
1
λ: funcId "hello"
"hello"
~~~

The second item in the Category typeclass is `(.)`, a function between arrows
that composes and sequences.  It takes `g :: a -> b` and `f :: b -> c` (in
reverse order) and creates a new function `a -> c` that first applies `f` and
then applies `g`:

~~~haskell
funcComp :: (->) b c -> (->) a b -> (->) a c
funcComp f g = \x -> f (g x)
~~~

~~~haskell
λ: ((+1) `funcComp` (*2)) 5
11
λ: (show `funcComp` not) True
"False"
~~~

Checking that these satisfy the category laws is a nice exercise, and involves
just substituting the definitions for eachother.

With this in mind, let's write a Category instance for `(->)`:

~~~haskell
instance Category (->) where
    id = \x -> x
    f . g = \x -> f (g x)
~~~

Et voilà!

### The `Auto` Category

Now let's create a Category instance for our Autos.

First, the identity arrow.  It is supposed to take an `a` and return an
identical `a` unchanged.

~~~haskell
autoId :: Auto a a
autoId = ACons $ \x -> (x, autoId)
~~~

~~~haskell
λ: testAuto_ autoId [1..10]
[1,2,3,4,5,6,7,8,9,10]
λ: testAuto_ autoId "hello world"
"hello world"
λ: testAuto_ autoId [True]
[True]
~~~

Perfect!  We created an Auto that does nothing and has no internal state ---
it only "dumb"ly returns the input as-is; the "head" is the same as the input
and the "tail" is just the same identity Auto.

Composition is a bit tricker.  Let's think a bit about what it would mean.

If we sequence `g :: Auto a b` and `f :: Auto b c`...we can think of it as
creating a big Auto with two states that keep on ticking.  `g` takes an `a`,
advances its state appropriately, and gives the resulting `b` to `f`.  `f`
takes that `b`, advances its own state, and pops out a `c` overall.  The
result is an `a` turning into a `c`, and both `g` and `f` updating their
state.

Let's take a deep breath and jump right into it.

~~~haskell
autoComp :: Auto b c -> Auto a b -> Auto a c
f `autoComp` g = ACons $ \x ->
  let (y, f') = runAuto f x
      (z, g') = runAuto g y
  in  (z, f' `autoComp` g')
~~~

To test this, let's make a simple Auto combinator `functionToAuto`, that takes
a pure function and turns it into a "pure Auto" --- that is, an Auto that
behaves exactly like that function and does not have any internal state.

~~~haskell
functionToAuto :: (a -> b) -> Auto a b
functionToAuto f = ACons $ \x -> (f x, functionToAuto f)
~~~

And let's test some compositions of our pure Autos:

~~~haskell
λ: let doubleAuto = functionToAuto (*2)
λ: let succAuto = functionToAuto (+1)
λ: let constAuto x = functionToAuto (const x)

λ: testAuto_ (succAuto `autoComp` doubleAuto) [1..10]
[3,5,7,9,11,13,15,17,19,21]

λ: testAuto_ (doubleAuto `autoComp` autoId) [1..10]
[2,4,6,8,10,12,14,16,18,20]

λ: testAuto_ doubleAuto [1..10]         -- f . id == f
[2,4,6,8,10,12,14,16,18,20]

λ: testAuto_ (succAuto `autoComp` (constAuto 20)) [1,2,undefined]
[21,21,21]
~~~

And how about we compose with some of our "impure" Autos from before?

~~~haskell
λ: testAuto_ summer [5,1,9,2,-3,4]
[5,6,15,17,14,18]

λ: testAuto_ doubleAuto [5,6,15,17,14,18]
[10,12,30,34,28,36]

λ: testAuto_ (doubleAuto `autoComp` summer) [5,1,9,2,-3,4]
[10,12,30,34,28,36]

λ: testAuto_ settableAuto [Nothing,Nothing,Just (-3),Nothing,Nothing]
[1,2,-3,-2,-1]

λ: testAuto_ summer [1,2,-3,-2,-1]
[1,3,0,-2,-3]

λ: testAuto (summer `autoComp` settableAuto)
  |    [Nothing,Nothing,Just (-3),Nothing,Nothing]
[1,3,0,-2,-3]
~~~

And now we can write a Category instance for Auto:

~~~haskell
instance Category Auto where
    id    = ACons $ \x -> (x, id)
    f . g = ACons $ \x ->
              let (y, f') = runAuto f x
                  (z, g') = runAuto g y
              in  (z, f' . g')
~~~

Now we can use Autos just like we use functions!

~~~haskell
λ: ((+1) . (*2)) 2
5

λ: testAuto_ (succAuto . doubleAuto) [2]
[5]

λ: ((+1) . id) 2
3

λ: testAuto_ (succAuto . id) [2]
[3]

λ: testAuto_ (summer . settableAuto . id)
  |  [Nothing,Nothing,Just (-3),Nothing,Nothing]
[1,3,0,-2,-3]
~~~

It's...a bit trippy at first, getting used to the fact that `id` and `(.)` are
now "overloaded" in the same sense that `(>>=)` and `return` or `mempty` and
`(<>)` are, but the more we work with multiple Category instances, the more we
get used to it :)

Of course remember that you have to explicily hide the `(.)` and `id` that
come in Prelude:

~~~haskell
import Prelude hiding ((.), id)
~~~

The Power of Typeclasses
------------------------

Let's step back for a second and see what we just did.  This is the true power
of Haskell typeclasses in action.  We just took two types that had completely
different implementations and representations...and now are able to talk about
both of them **using the same language**.

This is actually a pretty amazing thing and is a real triumph of the typeclass
approach to abstraction.

I talked about this in an earlier blog post [on MonadPlus][monadplus], where
we were able to use both Maybe and List in the exact same way by providing a
common language: `return` to represent success, and `mzero` to represent
failure.  We can use Maybe and List in the exact same way and reason about
them as if they were the "same thing", if we just used `return` and `mzero`.

[monadplus]: http://blog.jle.im/entry/the-list-monadplus-practical-fun-with-monads-part

For Functors, `fmap` for Maybe and `fmap` for List --- not to mention `fmap`
for IO and `fmap` for State --- have completely different implementations and
at the low level have nothing to do with eachother at all.  In many languages,
we would write a separate `fmap` function for every data structure --- a
`maybeMap`, an `ioMap`, a `stateMap`...

Yet, with typeclasses, we are now able to talk about all of these things in
the *exact same way* --- we capture the essential design pattern in `fmap (+1)
(Just 1)` and in `fmap (+1) [1,2,3]` and in `fmap (+1) readLn`.  We can use
operators like `(<$>)` and expect them to work the same high-level way for IO,
State, Maybe, List, etc.

You might have first learned that you can map over lists.  Then one day, you
realize that you can map over a *lot* of things...and now lists are longer any
"special" mappable object.  They are just one mappable thing out of many.

This is what we just did here.

You might have one day learned that you could compose functions `(.)` and have
identity functions `id`.  Then, one day (maybe that day is today!) you realize
that you can also compose and have identity arrows over...lots of things!  So
many things!  We haven't even scratched the surface of the wide variety of
useful `Category` instances!

Now, plain ol' `(->)` functions are no longer any "special" function things.
They are just one function-like thing out of many, many.

And now we can reason with *all* of them, as a whole, using `(.)` and `id`.
We can `(.)` and have `id` for many things.

If you're interested in this, you might want to look into the Kleisli
category, whose arrows are of type `Kleisli a b`, and which represent
functions `a -> m b` for monad `m`.  Using `Kleisli`, we can also use `(.)`
and `id` to reason with monads, as well as functions and Autos.

More Typeclasses!
-----------------

Now that we know how cool typeclasses are, let's take some time to try to
instance our Autos into the "cool" typeclasses to be in: [Functor,
Applicative, and Monad][fam].

[fam]: http://adit.io/posts/2013-04-17-functors,_applicatives,_and_monads_in_pictures.html

### Functor

What is a functor?  It represents something that can be mapped over.  More
pedantically, it is something that implements `fmap` and follows certain laws.

~~~haskell
fmap :: Functor f => (a -> b) -> f a -> f b
~~~

It doesn't really make sense for `Auto` to be a functor, because you are
mapping over both the input and the output?  What?

You can see this absurdify by trying to substitute `f ~ Auto` in the type
signature for `fmap`:

~~~haskell
fmap :: (a -> b) -> Auto a -> Auto b
~~~

`Auto a` isn't even a concrete type...so this doesn't really make too much
sense!

But it does make sense for `Auto i` to be a functor!

~~~haskell
fmap :: (a -> b) -> (Auto i) a -> (Auto i) b
~~~

Okay, what would this even mean?

`Auto i` is something that takes an `i` as an input.  `Auto i a` is something
that outputs an `a`.  So if `Auto i` is a functor...it means that I can turn
`Auto i a` into `Auto i b` with a function `a -> b`.  I "map over" the
"output".

So `Auto i` is a functor where you can `map` the output.  If I was going to
output a `5`, if I `fmap (+1)`, I'd actually output a `6`.

~~~haskell
instance Functor (Auto i) where
    fmap f a =  ACons $ \x ->
                  let (y  , a') = runAuto a x
                  in  (f y, fmap f a')
~~~

~~~haskell
λ: testAuto_ settableAuto
  |  [Nothing,Nothing,Just (-3),Nothing,Nothing]
[1,2,-3,-2,-1]

λ: testAuto_ (fmap (*2) settableAuto)
  |  [Nothing,Nothing,Just (-3),Nothing,Nothing]
[2,4,-6,-4,-2]
~~~

### Applicative

What is an Applicative functor?  It really is two things: the ability to apply
functions "inside" containers to values "inside" containers, and the ability
to wrap a value in a default context/container (all following the laws of
course).  The second part is going to be more useful to us.[^pointed]

[^pointed]: Actually this "wrapping" property really was kind of jammed into
Applicative, it technically belongs [Pointed][] typeclass, and Applicative is
technically only supposed to mean the function wrapping ability, under some
interpretations.  But splitting up typeclasses to such a fine degree isn't
quite practical.

[Pointed]: http://hackage.haskell.org/package/pointed-4.0/docs/Data-Pointed.html

~~~haskell
class Functor f => Applicative f where
    pure  :: a -> f a
    (<*>) :: f (a -> b) -> f a -> f b
~~~

Again, we see that it doesn't make too much sense for `Auto` to be an
Applicative...but `Auto i` definitely can be.

~~~haskell
pure :: a -> (Auto i) a
~~~

Can you think of a function that has this type signature?

As it turns out, due to [parametricity][], there is actually only exactly one
meaningful function that has this type signature.

[parametricity]: http://en.wikipedia.org/wiki/Parametricity

What must that `Auto i a` be?  Well, it clearly must output an `a`.  Can it
possibly incorporate `i` in any way?  It can't!  Because it can't really
"make" any `a`s besides the one given to it in `pure k`.  So that `pure k ::
Auto i a` must be an Auto that ignores its input `i` and always returns `k`
every time.

It must be the "constant" arrow.

~~~haskell
instance Applicative (Auto i) where
    pure k = ACons $ \_ -> (k, pure k)
    (<*>)  = undefined
~~~

~~~haskell
λ: testAuto_ (pure 5) [1..10]
[5,5,5,5,5,5,5,5,5,5]

λ: testAuto_ (summer . pure 5) [1..10]
[5,10,15,20,25,30,35,40,45,50]

λ: testAuto_ (pure 5 . summer) [1..10]
[5,5,5,5,5,5,5,5,5,5]
~~~

As it turns out, `pure k` is the same as the `constAuto k` that we defined
earlier.  Now we just have a more semantically meaningful way of constructing
it instead of using `functonToAuto`

I'll leave the implementation of `(<*>)` as an exercise, partially because
it's not too surprising, and mostly because `pure` is more interesting for the
time being.

Arrow
-----

As it turns out, `Category` by itself is nice, but for the games we will
eventually be playing with function composition, it doesn't offer too much in
terms of combinators.

There is a well-established Haskell extension that provides syntactic sugar
for complex, multi-way, side-chained compositions, called "proc notation".
Proc notation will prove invaluable to us eventually, but it requires some
Category combinators to work.

As it turns out, the `Arrow` typeclass exists as a general grab-bag of
combinators to make life a lot easier for us.

~~~haskell
class Category r => Arrow r where
    arr    :: (a -> b) -> r a b
    first  :: r a b -> r (a,c) (b,c)
    second :: r a b -> r (c,a) (c,b)
    (***)  :: r a b -> r c d -> r (a,c) (b,d)
    (&&&)  :: r a b -> r a c -> r a (b,c)
~~~

`arr` takes a normal function and turns it into a pure Auto --- we wrote this
before, it's just `functionToAuto`

`first` takes an Auto and turns it into an Auto that only operates on the
first part of a tuple.  `second` is the same, but for the second part.

`(***)` takes two Autos and makes an Auto that applies them "in parallel" to
two parts of a tuple.  `(&&&)` takes two Autos that both take the same type,
and makes an Auto that applies both Autos to the same value "in parallel" and
returns the results as a tuple.

~~~haskell
instance Arrow Auto where
    arr f     = ACons $ \x -> (f x, arr f)
    first a   = ACons $ \(x,y) ->
                  let (x', a') = runAuto a x
                  in  ((x', y), first a')
    second a  = ACons $ \(x,y) ->
                  let (y', a') = runAuto a y
                  in  ((x, y'), second a')
    a1 *** a2 = ACons $ \(x,y) ->
                  let (x', a1') = runAuto a1 x
                      (y', a2') = runAuto a2 y
                  in  ((x',y'), a1' *** a2')
    a1 &&& a2 = ACons $ \x ->
                  let (y1, a1') = runAuto a1 x
                      (y2, a2') = runAuto a2 x
                  in  ((y1,y2), a1' &&& a2')
~~~

Don't be too mystified by the Arrow typeclass.  Really, Arrows are just
Categories for which we have defined ways to chain compositions side-by-side.
Things like `(***)` and `(&&&)` are pretty useful if we want to be able to
compose multiple functions in fork-like ways, or to compose two functions
side-by-side with another function.  We will end up doing this a lot when we
work with AFRP, so this is pretty handy to have.

As it turns out, there are actually a lot of specialized Arrow typeclasses,
too, which are the same sort of "grab bag" of combinators, except for
different purposes.

The relevant ones we will be using later are:


